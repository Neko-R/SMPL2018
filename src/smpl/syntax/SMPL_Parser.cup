package smpl.syntax;
import smpl.semantics.*;
import smpl.values.*;
import smpl.syntax.IRExpProc.*;
import smpl.builtIn_necessities.*;

import java_cup.runtime.*;
import java.io.*;
import java.util.*;

/* Preliminaries to set up and use the scanner.  */
parser code {:
		//SmplLexer lexer;

		//InputStreamReader in = null;

		//public SmplParser(InputStream is) {
	     //   	in = new InputStreamReader(is);
		//}

		//public void report_error(String message, Object info) {
		 //   System.err.println(message);
		//}
        SmplLexer lexer = (SmplLexer) getScanner();
		public void syntax_error(Symbol cur_token) {
		    System.err.print("Line " + lexer.getLine() +
				     " near char " + lexer.getChar() + ": ");
		    report_error("Syntax error", cur_token);
		    System.err.println ("Last token read is " +
					 lexer.getText());
		}
	    :};
/*
init with {:
		// Create a lexer that reads from
		// supplied input stream or standard input
		if (in == null)
		   in = new InputStreamReader(System.in);
		lexer = new SmplLexer(in);
	  :};
*/
/*
scan with {:
		try {
		    return lexer.next_token();
		} catch (java.io.IOException ioe) {
		    System.out.println ("Unrecognised token");
		    System.out.println(ioe.getMessage());
		    throw ioe;
		}
	  :};
*/

/* Terminals (tokens returned by the scanner). */

// special symbols
terminal LPAREN, RPAREN, SEMI, COMMA, DOT, COLN;
terminal LBRACE, RBRACE, LBRAK, RBRAK, LVEC, RVEC;

// arithmetic operators
terminal PLUS, MINUS, MUL, DIV, MOD, EXPT, ASSIGN;

// relational operators
terminal EQV, GRT, LST, GQT, LQT, NQT;

// bitwise operators
terminal BAND, BOR, BNOT;

// logical operators
terminal AND, OR, NOT;

// list operation
terminal CAT;

// 'if' special form
terminal IF, THEN, ELSE;
terminal CASE;

// special key words
terminal LET, DEF, PROC, CALL;
terminal LAZY;

terminal PRINTLN, PRINT;
non terminal StmtPrint printStmt;

terminal READ, READINT;

//terminal COMNLN, COMMBLK;
// graphical operators (optional)


// terminals with values
terminal Integer INTEGER;
terminal String VARIABLE;
terminal String STRING;
terminal Character CHARACTER;
terminal Double DOUBLE;
terminal String TRU, FALS, NIL;

/* Non terminals */
non terminal SmplProgram program;
non terminal StmtSequence stmtList;
non terminal Statement stmt;
non terminal StmtDefinition definition;

non terminal IRExpMultiExp multiExp;

non terminal Read read;

/* Non terminals for LET */
non terminal IRExpLet letExp;

non terminal StmtDef defStmt;

non terminal IRExpIf ifExp;

non terminal IRExpCase caseExp;
non terminal Clause clause;
non terminal ArrayList<Clause> clauseLst;

non terminal ArrayList<Specification> specLst;
non terminal Specification spec;

non terminal IRExpProc procExp;
non terminal ParamLst paramList, paramListNE;
non terminal IRExp callProc;
non terminal ArrayList<IRExp> argList, List;
//non terminal ArrayList<String> vlist;

non terminal BindingList bindingList;
non terminal Binding binding;

non terminal IRExp logicOrExp, logicAndExp, logicNotExp;

non terminal IRExp expression;
non terminal IRExp arithExp;
non terminal IRExp term;
non terminal IRExp factor;
non terminal IRExp exponent;
non terminal IRExp unique;
non terminal empty;

non terminal IRExp relatExp, bitWiseExp, negat;

/* Grammar rules */

program ::= stmtList:s {: RESULT = new SmplProgram(s); :};

stmtList ::= stmtList:lst SEMI stmt:s {:
		lst.add(s);
		RESULT = lst;
		:} |
	     stmt:s {:
		RESULT = new StmtSequence(s);
		:};

/* ----- Add Statements below here ----- */

stmt ::= definition:d {: RESULT = d; :} |
          defStmt: ds {: RESULT = ds; :}|
          printStmt:ps {: RESULT = ps; :}|
          //COMMBLK:c {: RESULT = null;:}|
	      expression:e {: RESULT = new StmtExpr(e); :};

definition ::= factor:vlst ASSIGN expression:alst {: RESULT = new StmtDefinition(vlst, alst);:};

//vlist ::= vlist:alst COMMA factor:e {: alst.add(e); RESULT = alst; :}|
//            factor:e1 {: ArrayList<String> lst = new ArrayList<>(); lst.add(e); RESULT = lst; :};

defStmt ::=	  DEF VARIABLE:id expression:e {: RESULT = new StmtDef(id, e); :};

printStmt ::= PRINTLN LPAREN expression:e RPAREN {: RESULT = new StmtPrint(e, "line"); :}|
               PRINT LPAREN expression:e RPAREN {: RESULT = new StmtPrint(e, "regular"); :};

/* ----- Add Expressions below here ----- */

expression ::= logicOrExp:e {: RESULT = e; :}|
               letExp:ltstmt {: RESULT = ltstmt; :}|
                LBRACE stmtList:compexplst RBRACE {: RESULT = new IRExpCompExp(compexplst); :}|
                procExp: procstmt {: RESULT = procstmt; :}|
                ifExp: ifexp {: RESULT = ifexp; :}|
                caseExp:caseexp {: RESULT = caseexp; :};
                /* put other types here */

procExp ::= PROC LPAREN paramList:plst RPAREN stmt: body {: RESULT = new IRExpProc(plst, body, "fixed") ; :}|
            PROC LPAREN paramList:plst DOT VARIABLE:prest RPAREN stmt: body {: plst.add("norm",prest); RESULT = new IRExpProc(plst, body, "least") ; :}|
            PROC VARIABLE:id stmt:body {: ParamLst lst = new ParamLst("norm", id); RESULT = new IRExpProc(lst, body, "any"); :};

paramList ::= paramListNE:plst{: RESULT = plst; :}|
                   empty {: RESULT = new ParamLst(); :};

paramListNE ::= paramList:plst COMMA LAZY VARIABLE:pi {: plst.add("lazy", pi); RESULT = plst; :}|
                paramList:plst COMMA VARIABLE:pi {: plst.add("norm", pi); RESULT = plst; :}|
                 LAZY VARIABLE:p {: RESULT = new ParamLst("lazy", p); :}|
                 VARIABLE:p {: RESULT = new ParamLst("norm", p); :};

ifExp ::= IF logicOrExp:pred THEN stmt:thenClause ELSE stmt:elseClause {: RESULT = new IRExpIf(pred,thenClause, elseClause); :};

caseExp ::= CASE LBRACE clauseLst:clst COMMA ELSE COLN expression:e RBRACE {: RESULT = new IRExpCase(clst,e); :}|
            CASE LBRACE clauseLst:clst RBRACE {: RESULT = new IRExpCase(clst); :};

clause ::= expression:p COLN expression:c {: RESULT = new Clause(p,c); :};

clauseLst ::= clauseLst:lst COMMA clause:c {: lst.add(c); RESULT = lst; :}|
              clause:c  {: ArrayList<Clause> lst = new ArrayList<>(); lst.add(c); RESULT = lst; :};

letExp ::= LET LPAREN bindingList:blst RPAREN stmt:body {: RESULT = new IRExpLet(blst, body); :};

bindingList ::= bindingList:blst COMMA binding:b {: blst.add(b); RESULT = blst;:}|
                binding:b {: RESULT = new BindingList(b); :};

binding ::= VARIABLE:id EQV expression:e {: RESULT = new Binding(id, e); :};


logicOrExp ::= logicOrExp:loe OR logicAndExp:lae {: RESULT = new IRExpLO(loe,lae); :}|
                logicAndExp:lae {: RESULT = lae ; :};

logicAndExp ::= logicAndExp:lae AND logicNotExp:lne {: RESULT = new IRExpLA(lae,lne); :}|
                logicNotExp:lne {: RESULT = lne; :};

logicNotExp ::= NOT logicNotExp:lne {: RESULT = new IRExpLN(lne); :}|
                relatExp:re {: RESULT = re; :};

relatExp ::= relatExp:re EQV bitWiseExp:be {: RESULT = new IRExpEqv(re, be); :}|
              relatExp:re GRT bitWiseExp:be {: RESULT = new IRExpGrt(re, be); :}|
               relatExp:re LST bitWiseExp:be {: RESULT = new IRExpLst(re, be); :}|
             relatExp:re GQT bitWiseExp:be {: RESULT = new IRExpGqt(re, be); :}|
             relatExp:re LQT bitWiseExp:be {: RESULT = new IRExpLqt(re, be); :}|
             relatExp:re NQT bitWiseExp:be {: RESULT = new IRExpNqt(re, be); :}|
             bitWiseExp:be {: RESULT = be; :}|;

bitWiseExp ::= bitWiseExp:be BAND arithExp:ae {: RESULT = new IRExpBand(be, ae); :}|
                bitWiseExp:be BOR arithExp:ae {: RESULT = new IRExpBor(be, ae); :}|
                arithExp:ae {: RESULT = ae; :};

arithExp ::= 	arithExp:e PLUS term:t {:
			RESULT = new IRExpAdd(e, t); :} |
			arithExp:e CAT term:t {:
                RESULT = new IRExpCat(e, t); :} |
		arithExp:e MINUS term:t {:
			RESULT = new IRExpSub(e, t); :} |
		term:t {: RESULT = t; :};

term ::= term:t MUL exponent:expt {:
        RESULT = new IRExpMul(t, expt); :} |
	 term:t DIV exponent:expt {:
		RESULT = new IRExpDiv(t, expt); :} |
	 term:t MOD exponent:expt {:
		RESULT = new IRExpMod(t, expt); :} |
	 exponent:expt {: RESULT = expt; :};

exponent ::= negat:n EXPT exponent:expt {: RESULT = new IRExpExpt(n,expt); :}|
            negat: n {: RESULT = n; :};

negat ::=  BNOT negat:n {: RESULT = new IRExpBnot(n); :}|
            factor:f {: RESULT = f; :};

factor ::= INTEGER:ilit {: RESULT = new IRExpLit(ilit); :} |
        DOUBLE:ilit {: RESULT = new IRExpLit(ilit); :} |
        STRING:ilit {: RESULT = new IRExpLit(ilit); :} |
        CHARACTER:ilit {: RESULT = new IRExpLit(ilit); :} |
        TRU:ilit {: RESULT = new IRExpLit(true); :} |
        FALS:ilit {: RESULT = new IRExpLit(false); :} |
        VARIABLE:var {: RESULT = new IRExpVar(var); :} |
	   NIL:nil {: RESULT = new IRExpLit(); :} |
	   LPAREN expression:e RPAREN {: RESULT = e; :} |
	   multiExp:multiexplst {: RESULT = multiexplst; :}|
	   unique:u  {: RESULT = u; :}|
	   read:rs {: RESULT = rs; :}|
	   callProc:call {: RESULT = call; :};

unique ::=  LVEC specLst:slst RVEC {: RESULT = new IRExpVector(slst); :}|
            factor:v LBRAK factor:n RBRAK {: RESULT = new IRExpGetIndex(v, n);:};//|
            //factor:v LBRAK factor:n RBRAK ASSIGN expression:e {: RESULT = new IRExpGetIndex(v, n, e);:};

specLst ::= specLst:slst COMMA spec:sp {: slst.add(sp); RESULT = slst; :}|
            spec:sp {: ArrayList<Specification> lst = new ArrayList<>(); lst.add(sp); RESULT = lst; :};

spec ::= expression:e COLN procExp:p {: RESULT = new Specification(e,p); :}|
            expression:e {:  RESULT = new Specification(e); :};

callProc ::=  CALL LPAREN factor:f COMMA factor:alst RPAREN {: RESULT = new IRExpProcCallFull(f, alst); :}|
                factor:f LPAREN argList:alst RPAREN {: RESULT = new IRExpProcCallShort(f,alst); :}|
                LBRAK argList:alst RBRAK {: RESULT = new IRExpProcCallShort(new IRExpVar("list"), alst); :};

multiExp ::= LPAREN List:multiexplst RPAREN {: RESULT = new IRExpMultiExp(multiexplst); :};

List ::= List:alst COMMA expression:e {: alst.add(e); RESULT = alst; :}|
             expression:e1 COMMA expression:e {: ArrayList<IRExp> lst = new ArrayList<>(); lst.add(e1); lst.add(e); RESULT = lst; :};

argList ::= argList:alst COMMA expression:e {: alst.add(e); RESULT = alst; :}|
            expression:e {: ArrayList<IRExp> lst = new ArrayList<>(); lst.add(e); RESULT = lst; :};

read ::= READ LPAREN RPAREN {: RESULT = new Read("string"); :}|
            READINT LPAREN RPAREN {: RESULT = new Read("int"); :};

empty ::= ;